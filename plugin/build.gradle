plugins {
    id 'java'
    id 'nebula.ospackage'
    id "io.freefair.lombok"
    id 'jacoco'
}
apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.testclusters'
ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE')
}


opensearchplugin {
    name 'opensearch-ml'
    description 'machine learning plugin for opensearch'
    classname 'org.opensearch.ml.plugin.MachineLearningPlugin'
}

dependencies {
    compile project(':opensearch-ml-common')
    compile project(':opensearch-ml-algorithms')

    compile group: 'org.opensearch', name: 'opensearch', version: "${opensearch_version}-SNAPSHOT"
    compile("com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}")
    compile("com.fasterxml.jackson.core:jackson-databind:${versions.jackson}")
    compile group: 'com.google.guava', name: 'guava', version:'29.0-jre'

}

test {
    include '**/*Test.class'
    systemProperty 'tests.security.manager', 'false'
    finalizedBy jacocoTestReport
}

compileJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
}

//TODO: check which one should be enabled
licenseHeaders.enabled = false
testingConventions.enabled = false
checkstyleTest.enabled = false
forbiddenApis.ignoreFailures = false
dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
forbiddenApisTest.ignoreFailures = true
forbiddenApisMain.ignoreFailures = true
validateNebulaPom.enabled = false
checkstyleMain.enabled = false
loggerUsageCheck.enabled = false

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }

    dependsOn test
}

List<String> jacocoExclusions = [
        // code for configuration, settings, etc is excluded from coverage
        'org.opensearch.ml.plugin.MachineLearningPlugin',

        // Class containing just constants.  Don't need to test
        'org.opensearch.ml.constant.*',


        // related to transport actions
        'org.opensearch.ml.action.stats.MLStatsNodeRequest',
        'org.opensearch.ml.action.stats.MLStatsNodeResponse',
        'org.opensearch.ml.action.stats.MLStatsNodesAction',
        'org.opensearch.ml.action.stats.MLStatsNodesRequest',
        'org.opensearch.ml.action.stats.MLStatsNodesResponse'
]

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = jacocoExclusions
            limit {
                counter = 'LINE'
                minimum = 0.75
            }
        }
        rule {
            element = 'CLASS'
            excludes = jacocoExclusions
            limit {
                counter = 'BRANCH'
                minimum = 0.6
            }
        }
    }
    dependsOn jacocoTestReport
}
check.dependsOn jacocoTestCoverageVerification